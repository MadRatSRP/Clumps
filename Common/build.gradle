buildscript {
    repositories {
        maven {
            name = "BlameJared"
            url = 'https://maven.blamejared.com'
        }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath group: 'com.blamejared', name: 'ModTemplate', version: '2.+', changing: true
    }
}
plugins {
    id('java')
    id('org.spongepowered.gradle.vanilla') version '0.2.1-SNAPSHOT'
}

apply plugin: 'com.blamejared.modtemplate'
apply plugin: 'maven-publish'

import com.blamejared.modtemplate.Utils

archivesBaseName = "${mod_name}-common-${minecraft_version}"
version = Utils.updatingVersion(mod_version)
group = mod_group

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
}

minecraft {
    version(minecraft_version)
    runs {
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {

            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}
dependencies {
    compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.4'
}

publishing {

    publications {

        mavenJava(MavenPublication) {

            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java

            // Allows the maven pom file to be modified.
            pom.withXml {

                // Go through all the dependencies.
                asNode().dependencies.dependency.each { dep ->

                    println 'Surpressing artifact ' + dep.artifactId.last().value().last() + ' from maven dependencies.'
                    assert dep.parent().remove(dep)
                }
            }

        }
    }

    repositories {

        maven {
            project.properties

            url "file://" + System.getenv("local_maven")
        }
    }
}